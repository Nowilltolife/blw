allprojects {
    group 'dev.xdark'
    version '1.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        maven { url = 'https://jitpack.io' }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        testImplementation.extendsFrom implementation
        testImplementation.extendsFrom compileOnly
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:24.0.1'

        testImplementation platform('org.junit:junit-bom:5.9.3')
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    test {
        useJUnitPlatform()
    }

    // generate sources jar
    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier.set('sources')
    }

    publishing {
        repositories {
            mavenLocal()
            maven {
                name = 'sources-repo'
                url = 'file:///tmp/sources-repo'
            }
        }
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
            }
        }
    }
}
